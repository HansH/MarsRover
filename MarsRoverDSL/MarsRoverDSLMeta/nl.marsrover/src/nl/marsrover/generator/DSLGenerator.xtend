/*
 * generated by Xtext
 */
package nl.marsrover.generator

import nl.marsrover.dSL.Rule
import nl.marsrover.dSL.Specification
import org.eclipse.emf.ecore.resource.Resource
import org.eclipse.xtext.generator.IFileSystemAccess
import org.eclipse.xtext.generator.IGenerator
import nl.marsrover.dSL.Action
import nl.marsrover.dSL.Direction
import nl.marsrover.dSL.Condition

class DSLGenerator implements IGenerator {
	
	override void doGenerate(Resource resource, IFileSystemAccess fsa) {
		val spec = getSpecification(resource)
		val name = resource.URI.lastSegment().split("\\.", 2).get(0)
		val path = "generated/" + name + "/"
		
		fsa.generateFile(path + name + "Robot.java", generateMainClass(spec, name, resource))
		for(var i = 0; i < spec.rules.length; i++) {
			val ruleName = name + "Rule" + i
			fsa.generateFile(path + ruleName + ".java", generateRule(spec.rules.get(i), ruleName, name))
		}
	}
	
	
	def generateRule(Rule rule, String ruleName, String specName) '''
import java.io.IOException;
import lejos.robotics.subsumption.Behavior;


public class «ruleName» implements Behavior
{
	private boolean suppressed = false;
	private final «specName»Robot robot;
	private static final int TRESHOLD = 80;
	
	public «ruleName»(«specName»Robot robot)
	{
		this.robot = robot;
	}

	@Override
	public boolean takeControl()
	{
		return «rule.conditionList.conditions.join(" && ", [condition | generateCondition(condition)])»;
	}
	
	«IF rule.conditionList.conditions.exists[condition | condition.atLake]»
	private boolean atLake() {
		try {
			return robot.slave.readColor() > 1;
		}
		catch (IOException e) {
			e.printStackTrace();
		}
		return false;
	}
	«ENDIF»
	
	«IF rule.conditionList.conditions.exists[condition | condition.isNotProbed]»
	private boolean isProbed() {
		try {
			return robot.probed.containsKey(robot.slave.readColor());
		}
		catch (IOException e) {
			e.printStackTrace();
		}
		return true;
	}
	«ENDIF»

	@Override
	public void action()
	{
		this.suppressed = false;
		
		«FOR action : rule.actionList.actions»
		«generateAction(action)»
		«ENDFOR»
	}

	@Override
	public void suppress()
	{
		this.suppressed = true;
	}

}
	'''
	
	def generateCondition(Condition condition) '''
		«IF condition.not»
			!(«generateCondition(condition.condition)»)
		«ELSEIF condition.allLakes»
			robot.toProbe.isEmpty()
		«ELSEIF condition.collision»
			(robot.touchSensorLeft.isPressed() || robot.touchSensorRight.isPressed() || 
			robot.lightSensorLeft.getLightValue() > TRESHOLD || robot.lightSensorRight.getLightValue() > TRESHOLD)
		«ELSEIF condition.atLake»
			atLake()
		«ELSEIF condition.isNotProbed»
			!isProbed()
		«ENDIF»
	'''
	
	def generateAction(Action action) '''
		«IF action.showLakes»
			for (Integer lake : robot.probed.keySet()) {
				System.out.println(lake + " was " + robot.probed.get(lake));
			}
		«ELSEIF action.driveDirection»
		«IF action.direction == Direction.FORWARD»
			robot.pilot.forward();
		«ELSE»
			robot.pilot.backward();
		«ENDIF»
		
		«ELSEIF action.driveDistance»
			robot.pilot.travel(«IF action.direction == Direction.BACKWARD»-«ENDIF»«action.distance.value», true);
		«ELSEIF action.steer»
		«IF action.angle.away»
			robot.pilot.rotate(90, true);
		«ELSE»
			robot.pilot.rotate(«IF action.angle.sign»-«ENDIF»«action.angle.value», true);
		«ENDIF»
		«ELSEIF action.probeLake»
			try {
				robot.probed.put(robot.slave.readColor(), robot.slave.probeLake());
			}
			catch (IOException e) {
				e.printStackTrace();
			}
		«ELSEIF action.blinkLights»
			robot.slave.lampOn();
		«ENDIF»
		
		while (!this.suppressed && this.robot.pilot.isMoving()) {
			Thread.yield();
		}
		this.robot.pilot.stop();
	'''
	
	def generateMainClass(Specification spec, String name, Resource resource) '''
import lejos.robotics.subsumption.Behavior;

public class «name»Robot extends BaseRobot {
	
	public static void main(String[] args) {
		new «name»Robot().run();
	}
	
	public «name»Robot() {
		super();
	}

	@Override
	protected Behavior[] getBehaviors() {
		return new Behavior[] {
			«FOR i : 0..spec.rules.length - 1»
				new «name»Rule«i»(this),
			«ENDFOR»
		};
	}
}
	'''
	
	def getSpecification(Resource resource) {
		return resource.allContents.head as Specification
	}
	
}
